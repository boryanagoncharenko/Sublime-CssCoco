Semantic
ignore space, newline, comment, indent, tab
{
    find p=(id or class)
    require p.name match '^[a-z\-]+$'
    message 'All id and class names should be lowercase'

    find p=property{is_vendor_specific == false}
    require p.name match lowercase
    message 'Properties should be lowercase (vendor-specific properties are exception)'

    find value=(not string) in value
    require value.string match lowercase
    message 'All values except the contents of strings should be lowercase'

    find t=tag
    require t.name match lowercase
    message 'Html tags should be lowercase'

    find a=(attribute or attribute-value)
    require a.string match lowercase
    message 'Attribute selectors and their values should be lowercase'

    find d=declaration
    require d.child(-1).string == ';'
    message 'Put a ; at the end of declarations'

    forbid string in uri
    message 'Do not put quotes in url declarations'

    forbid hex{is_long and string match shorten}
    message 'Use short hex values'

    forbid ruleset{contains_all([
        property{name=='margin-right'},
        property{name=='margin-left'},
        property{name=='margin-top'},
        property{name=='margin-bottom'}
    ])}
    message 'Use the shorthand margin property instead'

    forbid ruleset{contains_all([
        property{name=='padding-right'},
        property{name=='padding-left'},
        property{name=='padding-top'},
        property{name=='padding-bottom'}
    ])}
    message 'Use the shorthand padding property instead'

    forbid number{num_value == 0} in (dimension or percentage)
    message 'Do not use units after 0 values'

    find n=number{num_value < 1 and num_value > -1}
    require n.string match '^0.*'
    message 'Use a leading zero for decimal values'

    find v=attribute-value
    require v is string and v.has_single_quotes
    message 'Use single quotes in attribute selectors'

    find s=string in charset
    require s.has_single_quotes
    message 'Use single quotes in charsets'

    find s=string in value
    require s.has_single_quotes
    message 'Use single quotes in values'

    forbid charset
    message 'Do not specify the encoding of style sheets as these assume UTF-8.'

    find u=uri
    require u.string not match '(?i)https?:.*'
    message 'Omit the protocol http(s) in url'

    forbid tag next-to (class or id)
    message 'Do not over-qualify classes and ids with html tags'
}

Whitespace
ignore newline indent comment, newline comment, indent, comment
{
    find r1=ruleset next-to r2=ruleset
    require newline{2} between r1 and r2
    message 'Put one blank line between rules'

    find c=colon next-to v=value
    require space between c and v
    message 'Put one space between the colon and the value of a declaration.'

    find s=selector next-to b=block
    require space between s and b
    message 'Put one space between the last selector and the block.'

    find s1=delim next-to s2=simple-selector
    require newline between s1 and s2
    message 'One selector per line.'
}

Custom
{
  forbid (space or indent) next-to (newline or eof)
  message 'No trailing spaces.'

  find i=indent
  require i.string match '^ {4}$'
  message 'Use 4 spaces for indentation, no tabs.'

  /* requires immediately-in and any
  find a=any in block
  require indent any{parent matches block}
  message "Indent the contents of a block"
  */
}

