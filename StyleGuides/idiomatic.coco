Semantic
ignore space, indent, tab, comment, newline
{
    find value=(not string) in value
    require value.string match lowercase
    message 'All values except the contents of strings should be lowercase'

    find d=declaration
    require d.child(-1).string == ';'
    message 'Put a ; at the end of declarations'

    forbid hex{is_long and string match shorten}
    message 'Use short hex values'

    forbid number{num_value == 0} in (dimension or percentage)
    message 'Do not use units after 0 values'

    find v=attribute-value
    require v is string and v.has_single_quotes
    message 'Use single quotes in attribute selectors'

    find s=string
    require s.has_single_quotes
    message 'Use single quotes in strings'
}

Whitespace
ignore newline indent comment, newline comment, indent, comment
{

    find r1=ruleset next-to r2=ruleset
    where not r1.is_single_line or not r2.is_single_line
    require newline{2} between r1 and r2
    message 'Add one blank line between rulesets'

    find r1=ruleset next-to r2=ruleset
    where r1.is_single_line and r2.is_single_line
    require newline{1, 2} between r1 and r2
    message 'Single_line rules may appear on adjacent lines'

    find c=colon next-to v=value
    require space between c and v
    message 'Put one space between the colon and the value of a declaration.'

    find s=selector next-to b=block
    require space between s and b
    message 'Put one space between the last selector and the block.'

    find s1=delim next-to s2=simple-selector
    require newline between s1 and s2
    message 'One selector per line.'

    find d=declaration
    require newline before d
    message 'Put every declaration on a new line'

    find b=block{count(declaration) != 1}
    require newline after b.child(0) and newline before b.child(-1)
    message 'Put newline after "{" and before "}"'

    find b=block{count(declaration) == 1}
    require (newline after b.child(0) and newline before b.child(-1)) or
            (space after b.child(0) and space before b.child(-1))
    message 'You can put spaces in one line declarations'

    find c=comma
    require space or newline after c
    message 'Multiple csv values should be separated by either space of newline'
}

Comments
ignore indent
{
    find c=comment
    require newline{1,} before c and newline{1,} after c
    message 'Place comments on a new line'
}
